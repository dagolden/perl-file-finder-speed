#!/usr/bin/env perl
use 5.12.0;
use Digest::MD5 qw(md5_hex);
use File::Path qw(make_path);
use Getopt::Long::Descriptive;

my ($opt, $usage) = describe_options(
  '%c %o [filecount]',
  [ 'target=s', 'name in which to build', { default => 'corpus' } ],
  [ 'splits=s',  'how to split up file; comma-lengths', { default => '2,2,2' } ],
);

warn "corpus already exists\n" if -d $opt->target;
make_path($opt->target);

my $max = $ARGV[0] || $usage->die;

my %files;
for (1 .. $max) {
  say;
  my $fn = md5_hex($_);

  my @dir;
  for my $len (split m{\s*,\s*}, $opt->splits) {
    push @dir, substr $fn, 0, $len, '';
  }

  my $target = join q{/}, $opt->target, @dir;
  make_path($target);
  open my $fh, '>', "$target/$fn" or die "can't open it: $!";
}

